using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Console;
using System.Linq;

using ReChord;

module Program
{
  Main(args : array[string]) : void
  {
    def input = if (args.Length == 0) Console.In else System.IO.File.OpenText(args[0]);
    def rdr = SimpleLineReader(input);
    def lines = Translate(ReadLines(rdr));
    OutputHtml(lines);
  }
  
  ReadLines(reader : SimpleLineReader) : IEnumerable.[Line]
  {
    while (reader.Read())
      yield reader.Current;
  }
  
  Translate(lines : IEnumerable.[Line]) : IEnumerable.[Line]
  {
    foreach(line in lines)
    {
      | chords is Line.Chords =>
        //yield chords;
        yield Translate(chords)
      | _ => yield line
    }
  }
  
  Translate(chords : Line.Chords) : Line.Chords
  {
    def newLine = ChordReplacer(chords.Text).Replace(Transposer(-2).Transpose);
    Line.Chords(newLine);
  }
  
  OutputHtml(mutable lines : IEnumerable.[Line]) : void
  {
    lines = lines.ToList();
    def title = lines.OfType.[Line.Lyric]().First().Text;
    def style =
@"
<style type='text/css'>
h1 {font-size: 14pt}
body {font-size: 12pt}
span.chord {font-family:lucida console, courier new, courier; color:blue}
span.lyric {font-family:lucida console, courier new, courier;}
h1 {font-family:verdana, helvetica; font-size: 14pt}
</style>
";
    Console.WriteLine($"<html><head><title>$title</title>$style</head><body>");
    OutputHtmlLines(lines);
    Console.WriteLine("</body></html>");
  }
  
  OutputHtmlLines(lines : IEnumerable.[Line]) : void
  {
    mutable prevLine : Line = null;
    foreach(line in lines)
    {
      OutputLine(line, prevLine);
      prevLine = line;
    }
  }
  
  OutputLine(line : Line, prevLine : Line) : void
  {
    Console.WriteLine(DescribeLine(line, prevLine));
  }
  
  DescribeLine(line : Line, prevLine : Line) : string
  {
    def text = HtmlEncode(line.Text);
    match(line, prevLine)
    {
      | (Line.Chords, _)                 => $"<span class='chord'>$text</span><br/>"
      | (Line.Lyric, Line.Chords)
      | (Line.Lyric, Line.Lyric)         => $"<span class='lyric'>$text</span><br/>"
      | (Line.Lyric, null)               => $"<h1>$text</h1>"
      | (Line.Lyric, _)                  => $"<h3>$text</h3>"
      | (Line.Separator, Line.Separator) => ""
      | (Line.Separator, _)              => "<br/>"
    }
  }
  
  static HtmlEncode(text : string) : string
  {
    text.Replace(" ", "&nbsp;")
  }
  
}